buildscript {
  ext {
    junit_platform_version = "1.0.1"
  }

  repositories {
    mavenCentral()
    jcenter()
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath "org.junit.platform:junit-platform-gradle-plugin:${junit_platform_version}"
    classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
  }
}

allprojects {
  group = "me.dmexe.telemetry"
  version = project.hasProperty('project.version') ?
      project.getProperty('project.version') :
      new File("${rootProject.rootDir}/VERSION").text.trim()
}

subprojects {
  apply plugin: 'com.github.ben-manes.versions'
  apply from: "../gradle/junit.gradle"
  apply from: "../gradle/checks.gradle"
  apply from: "../gradle/publish.gradle"

  task downloadDependencies {
    description "Pre-downloads *most* dependencies"
    doLast {
      project.logger.lifecycle("Retrieving dependencies for ${project.name}")
      configurations.getAsMap().each { name, config ->
        try {
          config.files
        } catch (e) {
          project.logger.info e.message
        }
      }
    }
  }
}


